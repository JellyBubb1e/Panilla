java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    implementation project(':panilla-api')
    implementation project(':panilla-paper-v1_20_6')
    implementation project(':panilla-paper-v1_21')
    implementation project(':panilla-paper-v1_21_3')
    compileOnly 'io.papermc.paper:paper-api:1.20.6-R0.1-SNAPSHOT' //use 1.20.6 Paper API
}

buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.gradleup.shadow:shadow-gradle-plugin:8.3.5'
    }
}

processResources {
    filesMatching('**plugin.yml') {
        expand(
                'version': project.parent.version
        )
    }
}

apply plugin: 'com.gradleup.shadow'

shadowJar {
    relocate("de.tr7zw.changeme.nbtapi", "com.ruinscraft.panilla.lib.nbtapi")

    archiveFileName.set("Panilla.jar")
    destinationDirectory.set(new File("../target/bukkit"))
}

build.dependsOn(shadowJar)